#Использовать gitrunner

Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Аргумент("URL", "", "Url репозитория-донора");

	Команда.Аргумент("PATH", "", "Путь к директории репозитория-донора")
					.ТСтрока()
					.Обязательный(Ложь)
					.ПоУмолчанию("");

	Команда.Опция("w write-json", Ложь, "Записать конфигурацию в json-файл").Флаговый();
	Команда.Опция("j json", "test.json", "Путь к json-файлу");

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	Сообщить("Инициализация репо-донора...");
	
	URLДонора = Команда.ЗначениеАргумента("URL");
	ПутьКДонору = Команда.ЗначениеАргумента("PATH");

	Донор = Новый ГитРепозиторий();
	Донор.УстановитьРабочийКаталог(ТекущийКаталог());
	
	Сообщить("Клонирование...");
	Донор.КлонироватьРепозиторий(URLДонора, ПутьКДонору);

	Если Команда.ЗначениеОпции("write-json") Тогда

		Сообщить("Сохранение в json...");
		ИмяФайла = Команда.ЗначениеОпции("json");
		СохранитьПараметрыВJSON(ИмяФайла, URLДонора, ПутьКДонору);

	КонецЕсли;
	
	Сообщить("Команда успешно выполнена.");

КонецПроцедуры

Процедура СохранитьПараметрыВJSON(Знач ИмяФайла, Знач Адрес, Знач Путь)

	ЧтениеJSON = Новый ЧтениеJSON();
	Попытка
		ЧтениеJSON.ОткрытьФайл(ИмяФайла);
		Параметры = ПрочитатьJSON(ЧтениеJSON);
	Исключение
		Параметры = ПараметрыПоУмолчанию();
	КонецПопытки;
	ЧтениеJSON.Закрыть();

	Путь = ?(ЗначениеЗаполнено(Путь), Путь, ИмяРепо(Адрес));
	Путь = ОбъединитьПути(ТекущийКаталог(), Путь);

	Донор = Параметры["Донор"];
	Донор.Вставить("Адрес", Адрес);
	Донор.Вставить("Путь", Путь);

	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.ОткрытьФайл(ИмяФайла);
	ЗаписатьJSON(ЗаписьJSON, Параметры);
	ЗаписьJSON.Закрыть();

КонецПроцедуры

Функция ПараметрыПоУмолчанию()

	Донор = Новый Структура("Адрес,Путь", "", "");

	Параметры = Новый Структура;
	Параметры.Вставить("Донор", Донор);
	Параметры.Вставить("Коммиты", Новый Массив);
	Параметры.Вставить("Файлы", Новый Массив);

	Возврат Параметры;
КонецФункции

Функция ИмяРепо(Знач Адрес)

	Адрес = СокрЛП(Адрес);
	НачалоИмени = СтрНайти(Адрес, "/", НаправлениеПоиска.СКонца) + 1;
	ТочкаГит = СтрНайти(Адрес, ".git", НаправлениеПоиска.СКонца);

	Возврат Сред(Адрес, НачалоИмени, ТочкаГит - НачалоИмени);
КонецФункции
