#Использовать asserts
#Использовать gitrunner
#Использовать ".."

Функция ПолучитьСписокТестов(Тестирование) Экспорт
	
	СписокТестов = Новый Массив;
	СписокТестов.Добавить("Клонирование");
	СписокТестов.Добавить("ЗаписьВJSON");

	Возврат СписокТестов;
КонецФункции

Процедура ПередЗапускомТеста()  Экспорт
КонецПроцедуры

Процедура ПослеЗапускаТеста()  Экспорт
КонецПроцедуры

Процедура Клонирование()  Экспорт
	КомандаInit = Новый КомандаInit();

	НастройкиКоманды = ТестовыеНастройки();
	НастройкиКоманды.ПутьКДонору = ПолучитьИмяВременногоФайла();

	КомандаInit.ВыполнитьПоНастройкам(НастройкиКоманды);

	ГитРепо = Новый ГитРепозиторий();
	ГитРепо.УстановитьРабочийКаталог(НастройкиКоманды.ПутьКДонору);

	Ожидаем.Что(ГитРепо.ЭтоРепозиторий(), "Во временном каталоге есть гит-репозиторий").Равно(Истина);
	Ожидаем.Что(СокрЛП(ГитРепо.ПолучитьТекущуюВетку()), "В репозитории есть ветка main").Равно("main");

	УдалитьФайлы(НастройкиКоманды.ПутьКДонору);
КонецПроцедуры

Процедура ЗаписьВJSON()  Экспорт
		
	КомандаInit = Новый КомандаInit();

	НастройкиКоманды = ТестовыеНастройки();
	НастройкиКоманды.ПутьКДонору = ПолучитьИмяВременногоФайла();
	НастройкиКоманды.СохранитьJSON = Истина;
	НастройкиКоманды.ИмяФайлаJSON = ПолучитьИмяВременногоФайла("json");
	
	КомандаInit.ВыполнитьПоНастройкам(НастройкиКоманды);

	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.ОткрытьФайл(НастройкиКоманды.ИмяФайлаJSON);
	Настройки = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();

	Ожидаем.Что(Настройки, "Настройки должны корректно прочитаться").ИмеетТип("Структура");
	Ожидаем.Что(Настройки.Донор, "Настройки должны содержать описание репо-донора").ИмеетТип("Структура");
	Ожидаем.Что(Настройки.Донор.Адрес, "URL донора должен быть заполненным").Равно(НастройкиКоманды.URLДонора);
	Ожидаем.Что(Настройки.Донор.Путь, "Путь к донору должен быть актуальным").Равно(НастройкиКоманды.ПутьКДонору);
	Ожидаем.Что(Настройки.Коммиты, "Коммиты должны быть массивом").ИмеетТип("Массив");
	Ожидаем.Что(Настройки.Коммиты.Количество(), "Коммиты должны быть пустым массивом").Равно(0);

	УдалитьФайлы(НастройкиКоманды.ПутьКДонору);
	УдалитьФайлы(НастройкиКоманды.ИмяФайлаJSON);
КонецПроцедуры

Функция ТестовыеНастройки()
	
	Настройки = Новый Структура();
	Настройки.Вставить("URLДонора", "http://github.com/Golovanoff/test-ci.git");
	Настройки.Вставить("ПутьКДонору", "");
	Настройки.Вставить("СохранитьJSON", Ложь);
	Настройки.Вставить("ИмяФайлаJSON", "");

	Возврат Настройки;
КонецФункции